Output vhs-glow.gif

Set Width 1600
Set Height 1040
Set Shell "fish"
Set Theme "Catppuccin-Mocha"
Set LoopOffset 10
Set LoopOffset 50.55%
Set Margin 10
Set MarginFill "#FFFFFF"
Set BorderRadius 10
Set WindowBar "Colorful"
Set WindowBarSize 40
Set WindowTitle "Hello, world!"
Set CursorBlink false

Sleep 1s

Type "glow"

Sleep 100ms

Enter

Sleep 1s

Hide
  Tab
  Type "/artichoke"
  Enter
  Down 2
Show

Sleep 500ms

Down 20

Hide
  Escape
  Type "l"
  Down 5
Show

Sleep 1s
Up@400ms 5

Hide
  Type "/ulysses"
  Enter
Show

Sleep 500ms

Down@200ms 20

Hide
  Escape
  Type "/"
Show

Sleep 500ms

Type@500ms "todo"
Sleep 1s

Hide
  Escape
  Type "/ulysses"
  Enter
Show

Sleep 500ms

Type@750ms "????"

Hide
  Escape
  Type "/artichoke"
  Enter
  Type "m"
  Ctrl+A
  Right 4
Show

Sleep 1s
Type@250ms "Tasty "
Sleep 1s

Hide
  Escape
  Down 5
  Type "m"
  Ctrl+U
Show

Sleep 1s
Type@150ms "Your new internet thing"
Sleep 3s

Hide Ctrl+C Show
